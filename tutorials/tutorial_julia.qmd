---
engine: julia
execute:
  warning: false
  output: false
---

## Introduction

The purpose of this tutorial is to demonstrate that by modelling the random error $\sigma$ we can estimate species-area relationships across the full spectrum of areas, i.e., without relying on introducing breakpoints.

The `julia` code provided here covers for the following steps:

1. Simulation of species richness for islands nested in archipelagos
2. Visual inspection of the simulated data
3. Modeling the species-area relationship
4. Validation of the results

## Set up

We begin by loading the required packages.

```{julia}
using Distributions
using Turing
using StatsPlots
using DataFrames
using Chain
using Statistics: quantile
using Random: Xoshiro
```

## Simulating data

We begin by setting the fixed parameters for the simulation.
In the notation below $c$ represents an archipelago intercept and $z$ the coefficient for island area $A$ onto species richness $S$.

This gives us Arrhenius law:

$$
S = c A^z
$$

Underlying the $c$ are the hyperparameters $\bar{c}$, the mean of all archipelago intercepts, and $\tau$ the variation of atoll intercepts.

The parameters which together will determine the random error $\sigma$ are the baseline error $\sigma_{base}$, $\gamma_{area}$ and $\gamma_{precip}$.

```{julia}
rng = Xoshiro(521)

n_archipelago = 16
c̄ = 0.0
τ = 0.5
c = rand(rng, Normal(c̄, τ), n_archipelago)
z = 3.0
γ_area = -0.5
γ_precip = -0.5
σ_base = 0.0
```

We then draw the number of islands for each archipelago from a Poisson distribution with $\lambda = 20$ and truncated at a lower bound of $4$. <!-- truncation is probably redundant given the distribution of Poisson(20) -->

```{julia}
trunc_pois = truncated(Poisson(20), lower=4)
islands_per_archipelago = rand(rng, trunc_pois, n_archipelago)
n_islands = sum(islands_per_archipelago)

archipelago = reduce(vcat, fill.(1:n_archipelago, islands_per_archipelago))
```

Finally, we simulate the species `richness` for each island by taking into account each archipelago's intercept and precipitation values.

```{julia}
area = rand(rng, Normal(0, 1), n_islands)
precip = rand(rng, Normal(0, 1), n_archipelago)

richness = let j = archipelago
	μ = @. c[j] + area * z
	σ = @. exp(σ_base + γ_area * area + γ_precip * precip[j])
	rand(rng, MvNormal(μ, σ))
end
```

## Model

We use the probabilistic programming language Turing.jl to specify the model.
The more lengthy but also more explicit model syntax reveals that the likelihood in our model directly matches the data-generating process.

```{julia}
@model function distributional_model(archipelago, area, precip)
	j = archipelago
	J = length(unique(archipelago))

	# Priors
	c̄ ~ Normal(0, 1)
	τ ~ Exponential(1)
	c ~ filldist(Normal(c̄, τ), J)

	z ~ Normal(0, 1)

	σ_base ~ Cauchy(0, 1)
	γ_area ~ Normal(0, 0.25)
	γ_precip ~ Normal(0, 0.25)

	# Likelihood
	μ = @. c[j] + z * area
	σ = @. exp(σ_base + γ_area * area + γ_precip * precip[j])
	richness ~ MvNormal(μ, σ)

	return (; μ, σ)
end
```

Using the model function `distributional_model()` we create the unconditioned model by passing the input data `archipelago`, `area`, and `precip`.
In the next step, we use the `condition` syntax `|` to condition the prior model on the observed outcome data.

```julia
prior_model = distributional_model(archipelago, area, precip)
conditioned_model = prior_model | (richness=richness,)
chn = sample(conditioned_model, NUTS(), 1000)
```

```{julia}
#| include: false
model = distributional_model(archipelago, area, precip)
conditioned_model = model | (richness=richness,)
chn = sample(conditioned_model, NUTS(), MCMCThreads(), 1000, 4)
```

Let's first inspect the means and quantiles for the parameter estimates.

```{julia}
#| output: true
DataFrame(describe(chn)[2])
```

The mean parameter estimates match the inputs to the simulation, but we should only move forward with this analysis after having validated that there were no computational issues with MCMC sampling.
If the sampling algorithms at the root of our inference were to show problematic behaviour, we should not interpret our results at all.

## Validation

Just like frequentist, linear models, Bayesian models also require some thorough investigation and validation before extracting and plotting the estimates.
This short tutorial is not intended to provide a comprehensive introduction into best practiceses of Bayesian model validation.
Instead, we refer interested readers to some key references, such as the [WAMBS checklist](https://doi.org/10.1037/met0000065).

For a more extensive toolkit of MCMC and other model diagnostics, see [ArviZ.jl](https://arviz-devs.github.io/ArviZ.jl/stable/api/diagnostics/) and [MCMCChains.jl](https://turinglang.org/MCMCChains.jl/stable/).
